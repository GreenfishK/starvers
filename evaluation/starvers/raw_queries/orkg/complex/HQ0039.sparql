PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX orkgr: <http://orkg.org/orkg/resource/>
PREFIX orkgc: <http://orkg.org/orkg/class/>
PREFIX orkgp: <http://orkg.org/orkg/predicate/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?ratio
WHERE {
  {
    SELECT (COUNT(?comparison) AS ?n_valid_comparisons)
    WHERE {
      ?comparison a orkgc:Comparison.
    }
  }
  {
    SELECT (COUNT(DISTINCT ?comparison_) AS ?n_comparisons_with_missing_class) {
      ?comparison_ orkgp:compareContribution ?contribution.
      FILTER(
        NOT EXISTS {
            ?comparison_ a orkgc:Comparison
          }
       )
     }
   }
   BIND(
     CONCAT(
       ROUND(
         xsd:double(?n_comparisons_with_missing_class) / xsd:double(?n_valid_comparisons + ?n_comparisons_with_missing_class) * 100
       ),
       " %"
     ) AS ?ratio
   )
}
